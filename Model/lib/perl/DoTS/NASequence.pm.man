
package GUS::Model::DoTS::NASequence; # table name
use strict;
use GUS::Model::DoTS::NASequence_Row;
use GUS::Model::DoTS::VirtualSequence;
use CBIL::Bio::SequenceUtils;
use Bio::Tools::SeqStats;
#use GUS30::GUS_utils::Sequence;

use vars qw (@ISA);
@ISA = qw (GUS::Model::DoTS::NASequence_Row);

my $debug = 0;

sub getSequence {
    my $self = shift;
    my $ss = $self->SUPER::getSequence();

    if (!defined($ss) && ($self->getSubclassView() eq 'VirtualSequence')) {
        return &GUS::Model::DoTS::VirtualSequence::getSequence($self);
    }
    return $self->SUPER::getSequence();
}

sub setSequence{
	my($self,$sequence,$doMonomer) = @_;
	print STDERR "setting sequence for ",$self->getClassName()," ",$self->getId(),"\nNew: $sequence\n\nOld: ",$self->get('sequence'),"\n\n" if $debug;
	$sequence =~ s/\s//g; ##removes any returns
	$sequence =~ tr/a-z/A-Z/;
  if($self->get('sequence') ne $sequence){
    $self->set('length',length($sequence)) unless $self->getLength() == length($sequence);
    unless($doMonomer) {
      $self->monomerCount($sequence);
    }
    $self->set('sequence',$sequence); 
  }
}

sub toFasta {
	my($self,$type) = @_;
	my $defline;
	if($type){  ##use source_id if available else use gusid
    $defline = ">".($self->get('source_id') ? $self->get('source_id') : $self->getId()).($self->get('secondary_identifier') ? "\|" . $self->get('secondary_identifier') : "")." ";
		$defline .= $self->get('description') if $self->get('description');
	}else{ 
		$defline = ">".$self->getId()."\|".($self->get('source_id') ? "\|" . $self->get('source_id') : '').($self->get('secondary_identifier') ? "\|" . $self->get('secondary_identifier') : "")." ";
		$defline .= $self->get('description') if $self->get('description');
  }
	return "$defline\n" . CBIL::Bio::SequenceUtils::breakSequence($self->getSequence());
}

##XML format for cap4
sub toCAML {
  my($self) = @_;
  my $caml;
  $caml = "  <SEQUENCE NAME=\"".$self->getId()."\">\n";
  $caml .= "    <BASE>\n".CBIL::Bio::SequenceUtils::breakSequence($self->getSequence(),74,'      ')."    </BASE>\n";
  ##print the quality values if exist
  if(exists $self->{quality}){ $caml .= "    <QUALITY>$self->{quality}</QUALITY>\n";}
  $caml .= "  </SEQUENCE>\n";
  return $caml;
}


sub monomerCount{
  my ($self, $naSeq)=@_;
  my $monomersHash;
  my $countA = 0;
  my $countT = 0;
  my $countC = 0;
  my $countG = 0;
  my $countOther = 0;

  my $seqobj = Bio::PrimarySeq->new(-seq=>$naSeq,
				    -alphabet=>'dna');
  my $seqStats  =  Bio::Tools::SeqStats->new(-seq=>$seqobj);

  $monomersHash = $seqStats->count_monomers();
  foreach my $base (keys %$monomersHash) {
    if ($base eq 'A'){
      $countA = $$monomersHash{$base};
    }
    elsif ($base eq 'T'){
      $countT = $$monomersHash{$base};
    }
    elsif ($base eq 'C'){
      $countC = $$monomersHash{$base};
    }
    elsif ($base eq 'G'){
      $countG = $$monomersHash{$base};
    }
    else{
      $countOther = $$monomersHash{$base};
    }
  }

  $self->set('a_count', $countA) unless $self->getACount() == $countA;
  $self->set('t_count', $countT) unless $self->getTCount() == $countT;
  $self->set('c_count', $countC) unless $self->getCCount() == $countC;
  $self->set('g_count', $countG) unless $self->getGCount() == $countG;
  $self->set('other_count', $countOther) unless $self->getOtherCount() == $countOther;

}

1;

